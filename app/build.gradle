apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {

    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.yfbx.mybrowser"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode buildCount()
        versionName "1.0.0"
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
        debug {
            ndk {
                abiFilters 'arm64-v8a', 'x86'
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def type = variant.buildType.name
            if (type == 'release') {
                outputFileName = "Broswer_${versionName}_${versionCode}.apk"
            } else {
                outputFileName = "Broswer_${type}_${versionCode}.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

/**
 * 获取Git 提交次数
 */
static def buildCount() {
    Process process = "git rev-list --count HEAD".execute()
    process.waitFor()
    return process.getText().toInteger()
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.8"

    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-rc1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    implementation 'com.github.yfbx-repo:x-adapter:1.1.2'
    implementation 'com.github.yfbx:ResultHelper:2.0.1-ktx'

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    //Litepal
    implementation 'org.litepal.android:kotlin:3.0.0'
    //Picasso
    implementation('com.squareup.picasso:picasso:2.71828') {
        exclude group: 'com.android.support'
    }

}
